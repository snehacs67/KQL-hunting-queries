Description: Identifies when a user is added and then removed to an Azure Key Vault access policy within a short time period 
  
tactics:
  - CredentialAccess
relevantTechniques:
  - T1555

Query: 
  AzureDiagnostics
  | where ResourceType == "VAULTS"
  | where OperationName == "VaultPatch"
  | where ResultType == "Success"
  | extend UserObjectAdded = addedAccessPolicy_ObjectId_g
  | extend AddedActor = identity_claim_http_schemas_xmlsoap_org_ws_2005_05_identity_claims_name_s
  | extend KeyAccessAdded = tostring(addedAccessPolicy_Permissions_keys_s)
  | extend SecretAccessAdded = tostring(addedAccessPolicy_Permissions_secrets_s)
  | extend CertAccessAdded = tostring(addedAccessPolicy_Permissions_certificates_s)
  | where isnotempty(UserObjectAdded)
  | project
  AccessAddedTime=TimeGenerated,
  ResourceType,
  OperationName,
  ResultType,
  KeyVaultName=Resource,
  AddedActor,
  UserObjectAdded,
  KeyAccessAdded,
  SecretAccessAdded,
  CertAccessAdded
  | join kind=inner 
  ( 
  AzureDiagnostics
  | where ResourceType == "VAULTS"
  | where OperationName == "VaultPatch"
  | where ResultType == "Success"
  | extend RemovedActor = identity_claim_http_schemas_xmlsoap_org_ws_2005_05_identity_claims_name_s
  | extend UserObjectRemoved = removedAccessPolicy_ObjectId_g
  | extend KeyAccessRemoved = tostring(removedAccessPolicy_Permissions_keys_s)
  | extend SecretAccessRemoved = tostring(removedAccessPolicy_Permissions_secrets_s)
  | extend CertAccessRemoved = tostring(removedAccessPolicy_Permissions_certificates_s)
  | where isnotempty(UserObjectRemoved)
  | project
  AccessRemovedTime=TimeGenerated,
  ResourceType,
  OperationName,
  ResultType,
  KeyVaultName=Resource,
  RemovedActor,
  UserObjectRemoved,
  KeyAccessRemoved,
  SecretAccessRemoved,
  CertAccessRemoved
  )
  on KeyVaultName
  | extend TimeDelta = abs(AccessAddedTime - AccessRemovedTime)
  | where TimeDelta < timeframe
  | project KeyVaultName, AccessAddedTime, AddedActor, UserObjectAdded, KeyAccessAdded, SecretAccessAdded, CertAccessAdded, AccessRemovedTime, RemovedActor, UserObjectRemoved, KeyAccessRemoved, SecretAccessRemoved, CertAccessRemoved, TimeDelta
  | extend AccountCustomEntity = UserObjectAdded
